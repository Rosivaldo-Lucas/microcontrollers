;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*              MODIFICAÇÕES PARA USO COM 12F675                   *
;*                FEITAS PELO PROF. MARDSON                        *
;*                   SETEMBRO DE 2023                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       NOME DO PROJETO                           *
;*                           CLIENTE                               *
;*         DESENVOLVIDO PELA MOSAICO ENGENHARIA E CONSULTORIA      *
;*   VERSÃO: 1.0                           DATA: 17/06/03          *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     DESCRIÇÃO DO ARQUIVO                        *
;*-----------------------------------------------------------------*
;*   MODELO PARA O PIC 12F675                                      *
;*                                                                 *
;*                                                                 *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ARQUIVOS DE DEFINIÇÕES                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#INCLUDE <p12f675.inc>	;ARQUIVO PADRÃO MICROCHIP PARA 12F675

	__CONFIG _BODEN_OFF & _CP_OFF & _PWRTE_ON & _WDT_OFF & _MCLRE_ON & _INTRC_OSC_NOCLKOUT

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    PAGINAÇÃO DE MEMÓRIA                         *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;DEFINIÇÃO DE COMANDOS DE USUÁRIO PARA ALTERAÇÃO DA PÁGINA DE MEMÓRIA
#DEFINE	BANK0	BCF STATUS,RP0	;SETA BANK 0 DE MEMÓRIA
#DEFINE	BANK1	BSF STATUS,RP0	;SETA BANK 1 DE MAMÓRIA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         VARIÁVEIS                               *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DOS NOMES E ENDEREÇOS DE TODAS AS VARIÁVEIS UTILIZADAS 
; PELO SISTEMA

	CBLOCK	0x20	;ENDEREÇO INICIAL DA MEMÓRIA DE
					;USUÁRIO
		W_TEMP		;REGISTRADORES TEMPORÁRIOS PARA USO
		STATUS_TEMP	;JUNTO ÀS INTERRUPÇÕES
		DADO		;ARMAZENA O DADO PARA A EEPROM

		CONT_I
		CONT_J
		VALOR_COMPARA
		
		;NOVAS VARIÁVEIS

	ENDC			;FIM DO BLOCO DE MEMÓRIA
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                        FLAGS INTERNOS                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS FLAGS UTILIZADOS PELO SISTEMA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         CONSTANTES                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODAS AS CONSTANTES UTILIZADAS PELO SISTEMA
; INICIALIZAÇÃO DA EEPROM, DE ACORDO COM A DESCRIÇÃO NO ARQUIVO "Def_Rega_Formigas.inc"

;A PARTIR DO ENDEREÇO ZERO DA EEPROM, DADOS EM ORDEM ALEATÓRIA
	ORG 0x2100
	DE	0X89,0X1E,0X39,0X9F,0XC2,0X0C,0XAB,0X33,0X63,0XD3,0X95,0X7B,0X38,0XD6,0X1E,0X48
	DE	0XDB,0XD8,0X86,0XFD,0XA5,0XFC,0X0C,0XBE,0X68,0X9B,0XD9,0X10,0XD8,0XEC,0X90,0X91
	DE	0XAA,0XBB,0XCC,0XDD,0XEE,0XF1,0XC9,0X77

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           ENTRADAS                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS PINOS QUE SERÃO UTILIZADOS COMO ENTRADA
; RECOMENDAMOS TAMBÉM COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           SAÍDAS                                *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÇÃO DE TODOS OS PINOS QUE SERÃO UTILIZADOS COMO SAÍDA
; RECOMENDAMOS TAMBÉM COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       VETOR DE RESET                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	ORG	0x00			;ENDEREÇO INICIAL DE PROCESSAMENTO
	GOTO	INICIO
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    INÍCIO DA INTERRUPÇÃO                        *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; ENDEREÇO DE DESVIO DAS INTERRUPÇÕES. A PRIMEIRA TAREFA É SALVAR OS
; VALORES DE "W" E "STATUS" PARA RECUPERAÇÃO FUTURA

	ORG	0x04			;ENDEREÇO INICIAL DA INTERRUPÇÃO
	MOVWF	W_TEMP		;COPIA W PARA W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP	;COPIA STATUS PARA STATUS_TEMP

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    ROTINA DE INTERRUPÇÃO                        *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; AQUI SERÃO ESCRITAS AS ROTINAS DE RECONHECIMENTO E TRATAMENTO DAS
; INTERRUPÇÕES

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                 ROTINA DE SAÍDA DA INTERRUPÇÃO                  *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; OS VALORES DE "W" E "STATUS" DEVEM SER RECUPERADOS ANTES DE 
; RETORNAR DA INTERRUPÇÃO

SAI_INT
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS		;MOVE STATUS_TEMP PARA STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W	;MOVE W_TEMP PARA W
	RETFIE

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*	            	 ROTINAS E SUBROTINAS                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; CADA ROTINA OU SUBROTINA DEVE POSSUIR A DESCRIÇÃO DE FUNCIONAMENTO
; E UM NOME COERENTE ÀS SUAS FUNÇÕES.
LE_EEPROM
;LER DADO DA EEPROM, CUJO ENDEREÇO É INDICADO EM W
;O DADO LIDO RETORNA EM W
	ANDLW	.127		;LIMITA ENDEREÇO MAX. 127
	BANK1				;ACESSO VIA BANK 1
	MOVWF	EEADR		;INDICA O END. DE LEITURA
	BSF		EECON1,RD	;INICIA O PROCESSO DE LEITURA
	MOVF	EEDATA,W	;COLOCA DADO LIDO EM W
	BANK0				;POSICIONA PARA BANK 0
	RETURN

GRAVA_EEPROM
;ESCREVE UM DADO NA EEPROM, CUJO ENDEREÇO É INDICADO EM W
;O DADO A SER ESCRITO DEVE ESTAR ARMAZENADO NA VARIÁVEL DADO
	ANDLW	.127		;LIMITA ENDEREÇO MAX. 127
	BANK1				;ACESSO VIA BANK 1
	MOVWF	EEADR
	MOVF	DADO,W
	MOVWF	EEDATA
	BSF		EECON1,WREN ;HABILITA ESCRITA
	BCF		INTCON,GIE	;DESLIGA INTERRUPÇÕES
	MOVLW	B'01010101'	;DESBLOQUEIA ESCRITA
	MOVWF	EECON2		;
	MOVLW	B'10101010'	;DESBLOQUEIA ESCRITA
	MOVWF	EECON2		;
	BSF		EECON1,WR ;INICIA A ESCRITA
AGUARDA
	BTFSC	EECON1,WR ;TERMINOU?
	GOTO	AGUARDA
	BSF		INTCON,GIE ;HABILITA INTERRUPÇÕES
	BANK0				;POSICIONA PARA BANK 0
	RETURN

SUBROTINA1

	;CORPO DA ROTINA

	RETURN

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIO DO PROGRAMA                          *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	
INICIO
	BANK1				;ALTERA PARA O BANCO 1
	MOVLW	B'00000000' ;CONFIGURA TODAS AS PORTAS DO GPIO (PINOS)
	MOVWF	TRISIO		;COMO SAÍDAS
	CLRF	ANSEL 		;DEFINE PORTAS COMO Digital I/O
	MOVLW	B'00000100'
	MOVWF	OPTION_REG	;DEFINE OPÇÕES DE OPERAÇÃO
	MOVLW	B'00000000'
	MOVWF	INTCON		;DEFINE OPÇÕES DE INTERRUPÇÕES
	BANK0				;RETORNA PARA O BANCO
	MOVLW	B'00000111'
	MOVWF	CMCON		;DEFINE O MODO DE OPERAÇÃO DO COMPARADOR ANALÓGICO

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIALIZAÇÃO DAS VARIÁVEIS                 *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ROTINA PRINCIPAL                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	;TEMPO DE EXECUÇÃO DO ALGORITMO: 3294709 (3,294709 s)
MAIN

	BSF	    GPIO,GP5		    ;SETA O BIT GP5 COMO UM, INDICA QUE A ORDENAÇÃO COMEÇOU
	
	MOVLW	    .1			    ;MOVE VALOR 1 PARA WORK. PRIMEIRO ELEMENTO A SER VERIFICADO
	MOVWF	    CONT_I		    ;MOVE O VALOR DE WORK PARA CONT_I
	CLRF	    CONT_J		    ;SETA CONT_J COMO ZERO
	CLRF	    VALOR_COMPARA	    ;VALOR_COMPARA COMEÇA COM ZERO
	
ORDENACAO_DECRESCENTE
	MOVLW	    .40			    ;MOVE 40 PARA O WORK. TAMANHO DA LISTA A SER ORDENADA
	SUBWF	    CONT_I,W		    ;W = CONT_I - W. SE CONT_I = 40, SAI DA ORDENAÇÃO
	BTFSC	    STATUS,C		    ;VERIFICA SE O BIT C É ZERO, SE ZERO, PULA PARA ORDENAÇÃO, SE NÃO, FINALIZA ORDENAÇÃO
	GOTO	    ORDENACAO_ACABOU	    ;SE O BIT C FOR ZERO, I_ITER JÁ É 40, CHEGOU AO FIM

	MOVFW	    CONT_I		    ;MOVE CONT_I PARA O WORK. ENDEREÇO A SER LIDO
	CALL	    LE_EEPROM		    ;LER DA EEPROM E ARMAZENA EM WORK
	MOVWF	    VALOR_COMPARA	    ;MOVE O VALOR LIDO PARA VALOR_COMPARA
	MOVFW	    CONT_I		    ;MOVE CONT_I PARA WORK
	MOVWF	    CONT_J		    ;MOVE WORK PARA CONT_J
	
COMPARAR_J_ATE_ZERO
	MOVLW	    .0			    ;MOVE ZERO PARA O WORK
	SUBWF	    CONT_J,W		    ;W = CONT_J - W.
	BTFSC	    STATUS,Z		    ;TESTA O BIT Z, SE FOR ZERO, ENTÃO PULA, SE NÃO EXECUTA A GRAVAÇÃO DO VALOR_COMPARA
	GOTO	    GRAVAR		    ;VAI PARA A GRAVAÇÃO DO VALOR_COMPARA
	MOVLW	    .1			    ;MOVE O VALOR UM PARA O WORK
	SUBWF	    CONT_J,W		    ;W = CONT_J - 1. PEGA O VALOR ANTERIOR A SER LIDO
	CALL	    LE_EEPROM		    ;LER DA EEPROM E GRAVA O VALOR LIDO EM WORK
	SUBWF	    VALOR_COMPARA,W	    ;W = VALOR_COMPARA - W. SE O BIT C FOR ZERO, ENTÃO WORK É MAIOR, ENTÃO NÃO TROCA
	BTFSS	    STATUS,C		    ;TESTA O BIT C, SE FOR ZERO, ENTÃO WORK É MAIOR E NÃO TROCA, SE BIT C FOR 1, ENTÃO TROCA, WORK MENOR
	GOTO	    GRAVAR		    ;VAI PARA GRAVAR O VALOR QUE NÃO IRÁ TROCAR
	MOVLW	    .1			    ;MOVE UM PARA O WORK
	SUBWF	    CONT_J,W		    ;W = CONT_J - 1. FAZ COM QUE O VALOR LIDO SEJA O ANTERIOR AO CONT_J
	CALL	    LE_EEPROM		    ;LER DA EEPROM E VALOR EM WORK
	MOVWF	    DADO		    ;MOVE O VALOR LIDO PARA DADO
	MOVFW	    CONT_J		    ;MOVE ENDERECO CONT_J, VALOR LIDO SERÁ TROCADO
	CALL	    GRAVA_EEPROM	    ;GRAVA VALOR NA POSIÇÃO CONT_J
	DECF	    CONT_J		    ;DECREMENTA O VALOR DE CONT_J
	GOTO	    COMPARAR_J_ATE_ZERO	    ;VOLTA PARA COMPARAR COM VALORES ANTERIORES
	
GRAVAR
	MOVFW	    VALOR_COMPARA	    ;MOVE VALOR_COMPARA PARA WORK
	MOVWF	    DADO		    ;MOVE VALOR_COMPARA PARA DADO
	MOVFW	    CONT_J		    ;MOVE ENDEREÇO QUE IRÁ RECEBER O VALOR_COMPARA
	CALL	    GRAVA_EEPROM	    ;GRAVA VALOR_COMPARA NO ENDEREÇO CONT_J
	INCF	    CONT_I		    ;INCREMENTA CONT_I. CONT_I VAI PARA A PRÓXIMA POSIÇÃO A SER COMPARADA
	GOTO	    ORDENACAO_DECRESCENTE   ;VOLTA PARA ORDENACAO_DECRESCENTE

ORDENACAO_ACABOU
	BCF	    GPIO, GP5		    ;FIM DE ORDENAÇÃO! BREAKPOINT
	GOTO	    ORDENACAO_ACABOU	    ;FIM DO PROCESSO DE ORDENACAO

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       FIM DO PROGRAMA                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	END
